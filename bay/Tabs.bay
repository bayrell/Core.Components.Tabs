/*!
 *  Bayrell Core Library
 *
 *  (c) Copyright 2018-2019 "Ildar Bikmamatov" <support@bayrell.org>
 *
 *  Licensed under the Apache License, Version 2.0 (the "License");
 *  you may not use this file except in compliance with the License.
 *  You may obtain a copy of the License at
 *
 *      https://www.apache.org/licenses/LICENSE-2.0
 *
 *  Unless reqvdomred by applicable law or agreed to in writing, software
 *  distributed under the License is distributed on an "AS IS" BASIS,
 *  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 *  See the License for the specific language governing permissions and
 *  limitations under the License.
 */
 
namespace Core.Components.Tabs;

use Runtime.Reference;
use Runtime.RuntimeUtils;
use Core.Annotations.Event;
use Core.Annotations.EventAsync;
use Core.Components.Tabs.TabsModel;
use Core.UI.Events.MountEvent;
use Core.UI.Events.MouseEvent.MouseClickEvent;
use Core.UI.Events.MouseEvent.MouseContextMenuEvent;
use Core.UI.Events.MouseEvent.MouseWheelEvent;
use Core.UI.Component;
use Core.UI.PocketModel;


class Tabs extends Component
{
	
	public TabsModel model;
	public mixed tabs = null;
	public mixed header_wrap = null;
	public mixed header = null;
	
	
	
	/**
	 * Returns model name
	 */
	lambda string modelName() => classof TabsModel;
	
	
	
	/**
	 * Component css
	 */
	lambda string css(Dict<string> vars) => 
		@css{
			%tabs{
				position: relative;
			}
			%tabs_header_wrap{
				overflow: hidden;
				white-space: nowrap;
				-webkit-overflow-scrolling: touch;
			}
			%tabs_header{
				position: relative;
			}
			%tab_header_item{
				display: inline-block;
				vertical-align: top;
				padding: 5px;
				cursor: pointer;
				-webkit-user-select: none;
				-moz-user-select: none;
				-khtml-user-select: none;
				-ms-user-select: none;
			}
			%tab_header_item.active{
				background-color: yellow;
			}
			%tabs_content_item{
				display: none;
			}
			%tabs_content_item.active{
				display: block;
			}
		}
	;
	
	
	
	/**
	 * Component render
	 */
	lambda Template render(TabsModel model, UIStruct vdom, PocketModel pocket) =>
	
		<div @class='tabs' @key="div" @ref="tabs" 
			data-active={ model.active != "" ? model.active : model.default_tab }
			@annotations=[
				new EventAsync
				{
					"event": classof MountEvent,
					"method_name": "onMountTabs",
					"animation": true,
				}
			]
		>
			{ static::renderHeader(model, vdom) }
			{ static::renderBottom(model, vdom) }
		</div>
	;
	
	
	
	/**
	 * Header render
	 */
	lambda Template renderHeader(TabsModel model, UIStruct vdom)
	{
		return 
			<div @class='tabs_header_wrap' @key="header_wrap" @ref="header_wrap"
				@annotations=[
					new Event
					{
						"event": classof MouseWheelEvent,
						"method_name": "onWheelTabs",
						"preventDefault": true,
						"animaton": true,
					},
				]
			>
				<div @class='tabs_header' @ref="header" style={ "left: "~model.left~"px" } 
					@watch=[ model.left, vdom.children ] >
				{ 
					vdom.children != null ? 
						vdom.children.map( 
							lambda UIStruct (UIStruct item) use (model) => 
								static::renderTab(item, model.active, model.default_tab) 
						) : ""
				}
				</div>
			</div>
		;
	}
	
	
	
	/**
	 * Header render
	 */
	lambda Template renderBottom(TabsModel model, UIStruct vdom) =>
	
		<div @class='tabs_content_wrap' @key="content">
			{
				vdom.children != null ? 
					vdom.children.map( 
						lambda UIStruct (UIStruct item) use (model) => 
							static::renderContent(item, model.active, model.default_tab) 
					) : "" 
			}
		</div>
	;
	
	
	
	/**
	 * Header render
	 */
	lambda Template renderTab(UIStruct item, string active, string default_tab)
	{
		string key = "";
		string title = "";
		string class_name = "";
		mixed tag = null;
		if (item.props)
		{
			key = item.key;
			title = item.props.get("title", "");
			tag = item.props.get("@tag", null);
		}
		if (key == active) class_name = "active";
		else if (active == "" and default_tab != "" and key == default_tab) class_name = "active";
		return new UIStruct{
			"space": RuntimeUtils::getCssHash(classof Tabs),
			"class_name": static::getCurrentClassName(), 
			"kind": "element",
			"name": "div",
			"key": key,
			"annotations": 
			[
				new Event{ "event": classof MouseClickEvent, "method_name": "onTaskHeaderClick" },
				new Event{ "event": classof MouseContextMenuEvent, "method_name": "onTaskHeaderContext" },
			],
			"props": {
				"@class": "tab_header_item",
				"@tag": tag,
				"class": class_name,
				"data-tab-key": key,
			},
			"children": [
				RuntimeUtils::normalizeUI(title),
			],
		};
	}
	
	
	
	/**
	 * Tab render
	 */
	lambda Template renderContent(UIStruct item, string active, string default_tab)
	{
		string key = "";
		string class_name = "";
		if (item.props) key = item.key;
		if (key == active) class_name = "active";
		else if (active == "" and default_tab != "" and key == default_tab) class_name = "active";
		return new UIStruct{
			"space": RuntimeUtils::getCssHash(classof Tabs),
			"class_name": static::getCurrentClassName(), 
			"kind": "element",
			"name": "div",
			"key": key,
			"props": {
				"@class": "tabs_content_item",
				"class": class_name,
				"data-tab-key": key,
			},
			"children": item.children,
		};
	}
	
	
	
	
	/* ---------------------------------- Events ----------------------------------- */
	
	
	/**
	 * Main click
	 */
	public async void onMountTabs(UIEvent<MountEvent> e)
	{
		this.header_wrap.style.width = this.tabs.offsetWidth ~ "px";
	}
	
	
	
	/**
	 * Returns true if component should updated
	 * @return bool
	 */
	public bool shouldUpdate()
	{
		return true;
	}
	
	
	
	/**
	 * Event click
	 */
	public void onTaskHeaderClick(UIEvent<MouseClickEvent> e)
	{
		/* Set new value */
		string key = e.vdom.props.get("data-tab-key");
		mixed tag = e.vdom.props.get("@tag", null);
		this.updateModel({
			"active": key,
			"active_tag": tag,
		});
	}
	
	
	
	public void onTaskHeaderContext(UIEvent<CoreEvent> e)
	{
		this.signalOut(e.event);
	}
	
	
	
	/**
	 * Wheel Event
	 */
	public void onWheelTabs(UIEvent<MouseWheelEvent> e)
	{
		var left = this.model.left + (e.event.wheelDelta / 5);
		var w = this.header.clientWidth - this.header.scrollWidth;
		if (left > 0) left = 0;
		if (left < w) left = w;
		/*this.header.style.left = this.left ~ "px";*/
		
		this.updateModel({
			"left": left,
		});
	}
	
	
}